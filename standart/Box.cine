type Box(item)
    private_item item
    private_haveItem Bool

rules
    1 = type == Box(_) & type[0] > Binary
    join 1 Binary
    2 = type == Box(_) & type[0] > Equal
    join 2 Equal
    3 = type == Box(_) & type[0] > StringReader
    join 3 StringReader
    4 = type == Box(_) & type[0] > StringWriter
    join 4 StringWriter

proc addEq(:data BinaryData, a Box(_))
    rules
        final = a > Binary
    data += a.private_haveItem
    if a.private_haveItem
        then data += a.private_item

func create(boxType Box(_), item boxType[0]) boxType
    attributes
        alwaysInline
    result:private_item = item
    result:private_haveItem = true

func empty?(a Box(_)) Bool
    attributes
        alwaysInline
    result = !a.private_haveItem

func equal(a Box(_), b a) Bool
    attributes
        alwaysInline
    rules
        final = a > Equal
    if result = a.item?() == b.item?(); result && a.item?()
        then result = a[] == b[]

func fromString(a Box(_), string [UInt8]) Box(a)
    rules
        final = a > StringReader
    if string.getLength() >= 2 && string[0] == '(' && string.getLast() == ')'
        then if itemString .= string.getAllExceptLast().getAllExceptFirst(); itemString.getLength() != 0
            then
                itemBox .= #a[0].fromString(itemString)
                if itemBox.item?()
                    then result:put(itemBox)
            else result:put(#a)

func getFromBinaryData(a Box(_), data BinaryData, :position UInt64) a
    rules
        final = a > Binary
    result:private_haveItem = Bool.getFromBinaryData(data, position)
    if result.private_haveItem
        then result:private_item = #a[0].getFromBinaryData(data, position)

func getItem(a Box(_)) a[0]
    attributes
        alwaysInline
    flag debug is
        "enable"
            panic(a.empty?(), "File: " builtIn_currentFile ", line: " builtIn_currentLine ". UB - get the value from the empty box.")
    if !a.empty?()
        then result = a.private_item

func item?(a Box(_)) Bool
    attributes
        alwaysInline
    result = a.private_haveItem

proc put(:box Box(_), item box[0])
    attributes
        alwaysInline
    box = #box.create(item)

func toString(a Box(_)) [UInt8]
    rules
        final = a > StringWriter
    if a.item?()
        then
            result = "(".join(a[].toString(), ")")
        else result = "()"
